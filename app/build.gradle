import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.zy.ppmusic"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 12
        versionName "1.1.2"
        resConfigs 'zh'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            buildConfigField("boolean", "IS_DEBUG", "true")
        }
        release {
            buildConfigField("boolean", "IS_DEBUG", "false")
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def time = new SimpleDateFormat("yyyyMMddHHmmss", Locale.CHINA).format(new Date())
                if (variant.buildType.name == 'release') {
                    String name = "love_music_r_${variant.versionCode}_" + time
                    if (!variant.signingReady) {
                        name += "_unsigned"
                    }
                    name += ".apk"
                    output.outputFileName = name
                }
                if (variant.buildType.name == 'debug') {
                    String name = "love_music_d_${variant.versionCode}_${time}.apk"
                    output.outputFileName = name
                }
            }
        }
    }
    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
        }
    }
    greendao {
        schemaVersion 1
        daoPackage 'com.zy.ppmusic.data.db.dao'
        targetGenDir 'src/main/java'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    kotlin {
        experimental {
            coroutines "enable"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //    java测试库
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    //一些测试库
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.19.0'
    testImplementation 'org.powermock:powermock-module-junit4:1.7.3'
    testImplementation 'org.powermock:powermock-module-junit4-rule:1.7.3'
    testImplementation 'org.powermock:powermock-api-mockito2:1.7.3'
    testImplementation 'org.powermock:powermock-classloading-xstream:1.7.3'
    //    系统库
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    //    Glide相关 显示专辑图片
    implementation 'com.github.bumptech.glide:glide:4.7.1'
    //    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'
    //  kotlin支持 双引号代表可以使用表达式
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:0.22.5'
    //    constraint库
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    //    greendao数据库
    implementation 'org.greenrobot:greendao:3.2.2'
    //    权限申请库
    implementation project(':easypermissions')
    //ViewModel and LiveData
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    //leak检测
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
}

